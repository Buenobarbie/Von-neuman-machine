< MNEM2OP  ; IMPORT FUNCTION OF LAB 07

JP READBGN ; JUMP TO BEGIN OF CODE

& /140 ; LEAVE 0002 - 012E FREE

; <LABEL1><LABEL1> <S><S> <MNEM><MNEM> <S><S> <LABEL2><LABEL2> <S><S> <COMMENT>...
; MAXIMO DE 50 LINHAS -> MAX DE 100 ESPAÃ‡OS PARA ARMAZENAR CADA PARTE

;------------------- VARIABLES -----------------
LISTLABEL1 K /2            ; ADDRESS LIST BEGINING       (2)
LISTLABEL1END K /66        ; ONE MORE THEN THE LAST ONE (102)

LISTMNEM K /66            ; ADDRESS LIST BEGINING      (102)
LISTMNEMEND K /CA         ; ONE MORE THEN THE LAST ONE (202)

LISTLABEL2 K /CA           ; ADDRESS LIST BEGINING      (202)
LISTLABEL2END K /12E       ; ONE MORE THEN THE LAST ONE (302)

LISTSMANIUPALTION K =2    ; FIRST ADDR TO MANIPULATE   (2)
LISTSMANIUPALTIONEND K =0 ; LAST ADDR TO MANIPULATE + 2 (TO BE SET)

MEMO K /9000              ; MEMORIZE OPCODE
LOAD K /8000              ; LOAD OPCODE

TEMP K =0 ; SAVE VALUES FOR FUTURE RELOCATION
SPACECOUNT K =0 ; AMOUNT OF SPACES IN ONE LINE 

;-------------- VARIABLES FOR NUMBERS ---------------
; HEXA
K0 K =0
K1 K =1
K2 K =2
K10  K /10
K100 K /100
K1000 K /1000
K30 K /30
K41 K /41
K9  K =9
K5 K =5
D11 K =11

; DECIMAL
D10 K =10
D100 K =100 ; DISTANCE OF PARTS FROM THE SAME LINE ON THE LISTS

;--------------- ASCII VARIABLES ------------------------
ENDLINE K /200A ; <s>\n LINE END
SPACE K /2020 ; <s><s>
ENDFILE K /0000 ; PROGRAM END 


; ------------------- READING FILE  AND SAVING DATA --------------------
READBGN GD /0300 
; CHECKS WHERE TO SAVE BASED ON THE AMOUNT OF SPACES READ
    MM TEMP       ; SAVES VALUE TEMPORARILY WHILE LOOKING FOR THE RIGHT PLACE TO SAVE IT
    JZ READEND    ; EFO = 0000
    SB SPACE      ; CHECK FOR SPACE
    JZ JPSPACE    
    LD SPACECOUNT
    JZ LABEL1     ; IF SPACECOUNT = 0 -> LABEL1
    SB K1      
    JZ MNEM       ; IF SPACECOUNT = 1 -> MNEM
    SB K1 
    JZ LABEL2     ; IF SPACECOUNT = 2 -> LABEL2
    SB K1
    JZ JPCOMMEnTS ; IF SPACECOUNT = 3 -> COMMENT

    ; ---- UPDATE SPACECOUNT ----
    JPSPACE LD SPACECOUNT 
        AD K1
        MM SPACECOUNT
        JP READBGN
    
    ; ----- SAVES LABEL1 ------
    LABEL1 LD LISTLABEL1     ; GET ADDRESS TO SAVE 
        AD MEMO 
        MM SAVELABEL1 
        LD TEMP 
        SAVELABEL1 K =0      ; SAVES ON THE RIGHT ADDRESS
        LD LISTLABEL1 
        AD K2                ; UPDATE ADDRESS
        MM LISTLABEL1
        JP READBGN
    
    ; ----- SAVES MNEM ------
    MNEM LD LISTMNEM         ; GET ADDRESS TO SAVE 
        AD MEMO
        MM SAVEMNEM
        LD TEMP
        SAVEMNEM K =0        ; SAVES ON THE RIGHT ADDRESS
        LD LISTMNEM 
        AD K2                ; UPDATE ADDRESS
        MM LISTMNEM
        JP READBGN

    ; ----- SAVES LABEL2 ------
    LABEL2 LD LISTLABEL2     ; GET ADDRESS TO SAVE 
        AD MEMO
        MM SAVELABEL2
        LD TEMP
        SAVELABEL2 K =0      ; SAVES ON THE RIGHT ADDRESS
        LD LISTLABEL2 
        AD K2                ; UPDATE ADDRESS 
        MM LISTLABEL2
        JP READBGN
    
    ; ------- IGNORE COMMENTS -----
    ; READS COMMENT UNTIL REACH END OF LINE
    JPCOMMENTS GD /0300 
        JZ READEND    ; EFO = 0000
        SB ENDLINE
        JZ SPACEZERO
        JP JPCOMMENTS

        ; RESET SPACE COUNT
        SPACEZERO LD K0 
            MM SPACECOUNT
            JP READBGN

; ALL DATA READ AND SAVED

; ------------- DATA MANIPULATION --------------
READEND LD LISTLABEL1 ; GET LAST ADDRES WRITTEN + 2 (NUMBER OF LINES + 1)
MM LISTSMANIUPALTIONEND 
LISTMBGN LD LISTSMANIUPALTION 
        SC WRITEADDRESS     ; WRITE ADDRESS OF INSTRUCTION
        LD SPACE         
        PD /301             ; WRITE <s><s>

        ; GET MENUMONIC OF INSTRUCTION
        LD LISTSMANIUPALTION 
        AD D100
        AD LOAD
        MM READMNEM
        READMNEM K =0
        SC MNEM2OP          ; CONVERT MENUMONIC TO OPCODE
        ML K1000
        MM TEMP             ; SAVES OPCODE TO GET OPERAND

        ; GET LABEL2 (OPERAND)
        LD LISTSMANIUPALTION 
        AD D100
        AD D100
        AD LOAD
        MM READLABEL2
        READLABEL2 K =0
        SC GETINSTRUCTION   ; FIND THE REFERENCE (LABEL1), SUM TEMP (OPCODE) AND WRITE ON FILE

        LD ENDLINE
        PD /301           ; WRITE <s>\n

        LD LISTSMANIUPALTION
        AD K2               ; GO TO NEXT LINE
        MM LISTSMANIUPALTION
        SB LISTSMANIUPALTIONEND
        JZ LISTMEND         ; CHECK IF REACHED END OF LINES
        JP LISTMBGN

LISTMEND LD ENDFILE ; WRITE END OF FILE
        PD /301   ; WRITE
        HM /00DE  ; END CODE

; ------ TASK WRITEADDRESS -------
WRITEADDRESS K =0 
    SB K2         ; LISTSMANIUPALTION STARTS AT 2
    SC NUMTOASCII ; WRITE ADDRESS IN ASCII ON FILE
RS WRITEADDRESS

FINDING K =0
LISTRUN K /2 ; ADDRESS LIST BEGINING
GETINSTRUCTION K /0000
    MM FINDING
    LD K2
    MM LISTRUN
    FINDBGN LD LISTRUN
        AD LOAD
        MM READLABEL1
        READLABEL1 K =0 ; READ LABEL1
        SB FINDING      ; CHECK IF EQUAL
        JZ FINDED       
        LD LISTRUN 
        AD K2           ; KEEP SEARCHING
        MM LISTRUN
        JP FINDBGN

    FINDED LD LISTRUN 
            SB K2         ; LISTRUN STARTS AT 2
            AD TEMP       ; SUM TO OPCODE
            SC NUMTOASCII ; CONVERT AND WRITE ON FILE
RS GETINSTRUCTION



; ------ VARIABLES FOR TASK NUMTOASCII -------
NUM  K =0
NUM1 K =0
NUM2 K =0
NUM3 K =0
NUM4 K =0
K8000 K /8000
K8   K =8


; --------- TASK NUMTOASCII ----------
; CONVERT HEX ADDRESS TO ASCII VALUES AND WRITE ON FILE
; EXAMPLE: 367F -> 3336 3746

NUMTOASCII K /0000  
    MM NUM ; SAVE NUMBER TO BE CONVERTED (367F)

    GETNUM1 SC ROUND    ;
            SC CONVERT  ; GET ASCII
            ML K100     ; SHIFT LEFT (0033) -> (3300)
            MM NUM1     ; SAVES IN NUMBER 1 (3300)

    GETNUM2 LD NUM
            ML K10     ; GET SECOND NUMBER OF ADDRESS 
            SC ROUND   ;
            SC CONVERT ; GET ASCII
            MM NUM2    ; SAVES IN NUMBER 2 (0036)

    GETNUM3 LD NUM
            ML K100    ; GET THIRD NUMBER OF ADDRESS 
            SC ROUND   ;
            SC CONVERT ; GET ASCII
            ML K100    ; SHIFT LEFT (0037) -> (3700)
            MM NUM3    ; SAVES IN NUMBER 3 (3700)

    GETNUM4 LD NUM
            ML K1000   ; GET FOURTH NUMBER OF ADDRESS   
            SC ROUND   ;
            SC CONVERT ; GET ASCII
            MM NUM4    ; SAVES IN NUMBER 4 (0046)     

    WRITECONVERTED LD NUM1
            AD NUM2    ; (3637)
            PD /301  ; WRITES
            LD NUM3   
            AD NUM4    ; (3746)
            PD /301  ; WRITES
RS NUMTOASCII


ROUND K /0000
        SB k8000;
        JN NOERROR;
        DV K1000;
        AD K8   ;
        JP ENDROUND;
    NOERROR AD K8000;
            DV K1000;
ENDROUND RS ROUND



    
; -------- TASK CONVERT --------

CONVERT K /0000
            SB D10         ; SUBTRACT 10 (DECIMAL)
            JN NUMBER      ; IF < 10 IS A NUMBER
            JP LETTER      ; IF >= 10 IS A LETTER (A-F);

    NUMBER  AD D10    ; GET NUMBER (LAST ACTION WAS SB D10)
            AD K30    ; 0 IN ASCII = 30
            JP ENDCONVERT

    LETTER  AD K41   ; A IN ASCII = 41
            JP ENDCONVERT

;; -----------------------------------------------------
 ;   JN LETTERAND8      ; IF < 0 IS BTW 8 AND F
 ;   JP NUMBERMAX6      ; IF >= 0 IS A NUMBER BTW 0 AND 7 (A-F);
;
 ;   NUMBER8 AD D10
 ;   NUMBERMAX6 AD K30    ; 0 IN ASCII = 30
 ;           JP ENDCONVERT
;
 ;   LETTERAND8 AD K5   ; -8 -> 8, -7 -> 9...
 ;           ; SB D10      ; SUBTRACT 10 (DECIMAL)
 ;           JN NUMBER8  ; IF < 0 IS 8
 ;           AD K41      ; A IN ASCII = 41
 ;           JP ENDCONVERT
;
ENDCONVERT RS CONVERT
        